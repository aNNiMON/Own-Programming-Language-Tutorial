use std, types, files, json, yaml, functional

INPUT_PATH_FMT = "./modules/%s.yml"
OUTPUT_DIR_FMT = "../docs/%s/modules"
OUTPUT_PATH_FMT = OUTPUT_DIR_FMT + "/%s.md"

LANGS = ["en", "ru"]
MODULES = [
  "std",
  "collections",
  "date",
  "downloader",
  "files",
  "functional",
  "http",
  "java",
  "math",
  "ounit",
  "regex",
  "robot",
  "socket",
  "types",
  // formats
  "base64",
  "json",
  "yaml",
  "gzip",
  "zip"

  // Desktop-only
  "canvas",
  "canvasfx",
  "forms",
  "jdbc"

  // Android-only
  "android",
  "canvas_android",
  "forms_android",
  "gps_android",
  "imageprocessing_android"
]

messages = {
    "constants": {"en": "Constants", "ru": "Константы"},
    "functions": {"en": "Functions", "ru": "Функции"},
    "types": {"en": "Types", "ru": "Типы"},
    "example": {"en": "Example", "ru": "Пример"},
    "since": {"en": "Since", "ru": "Начиная с"},
    "elements": {"en": " elements", "ru": " элементов"}
}

// Write modules pages to vuepress config
modulesPages = jsonencode(map(MODULES, def(m) = m + ".md"))
f = fopen("../docs/.vuepress/configs/modules.js", "w")
writeLine(f, "export default " + modulesPages)
flush(f)
fclose(f)

// Create output dirs
for lang : LANGS {
  mkdirs(sprintf(OUTPUT_DIR_FMT, lang))
}

for moduleName : MODULES {
  module = readYml(sprintf(INPUT_PATH_FMT, moduleName))
  for lang : LANGS {
    println "" + module.name + " / " + lang
    file = sprintf(OUTPUT_PATH_FMT, lang, moduleName)
    f = fopen(file, "w")

    writeHeader(f, module, lang)
    writeConstants(f, module.constants ?? [], lang)
    writeFunctions(f, module.functions ?? [], lang, 2);
    writeTypes(f, module.types ?? [], lang);

    flush(f)
    fclose(f)
  }
}

// -- write
def writeHeader(f, module, lang) {
  writeText(f, header(module.name, 1))
  if length(module.scope ?? "") && (module.scope != "both") {
    writeText(f, " (" + module.scope + ")")
  }
  writeLine(f, "\n")
  if length(module.since ?? "") {
    writeSince(f, module.since, lang)
  }
  writeDescription(f, module, lang, "\n%s\n")
}

def writeConstants(f, constants, lang) {
  if (constants.isEmpty()) return 0

  writeLine(f, "\n\n## " + messages.constants[lang])
  for info : constants {
    writeText(f, "\n`%s` : *%s*".sprintf(info.name, info.typeName))
    writeScope(f, info.scope ?? "")
    writeText(f, " = ")
    constValue = getValue(info, "value", lang)
    if (info.type != MAP && info.type != CLASS) {
      writeText(f, "`%s`".sprintf(constValue))
    } else {
      mapValues = constValue.substring(1, constValue.length - 1).split(", ")
      if (mapValues.length >= 7) {
        writeText(f, "\n\n::: details %d %s".sprintf(mapValues.length, messages.elements[lang]));
        writeText(f, "\n\n```own:no-line-numbers\n{\n  ");
        writeText(f, mapValues.joinToString(",\n  "));
        writeText(f, "\n}\n```");
        writeText(f, "\n:::");
      } else {
        writeText(f, "`%s`".sprintf(constValue));
      }
    }
    writeLine(f, "")
    writeDescription(f, info, lang, "\n%s\n")
  }

}

def writeFunctions(f, functions, lang, level = 2) {
  if (functions.isEmpty()) return 0

  writeLine(f, "\n\n" + header(messages.functions[lang], level))
  for info : functions {
    writeText(f, "\n`%s(%s)`".sprintf(info.name, info.args))
    writeScope(f, info.scope ?? "")
    if length(info.since ?? "") {
      writeSince(f, info.since, lang, true)
    }
    writeDescription(f, info, lang, " — %s")
    writeLine(f, "")

    example = getValue(info, "example", lang)
    if (length(example ?? "")) {
      writeLine(f, "\n```own")
      writeLine(f, example)
      writeLine(f, "```")
    }
  }
}

def writeTypes(f, types, lang) {
  if (types.isEmpty()) return 0

  writeLine(f, "\n\n" + header(messages.types[lang]))
  for info : types {
    writeText(f, "\n\n" + header("`%s`".sprintf(info.name), 3))
    writeScope(f, info.scope ?? "")
    writeDescription(f, info, lang, "%s\n")
    writeFunctions(f, info.functions ?? [], lang, 4);
    writeLine(f, "")
  }
}

def writeScope(f, scope) = match(scope) {
  case "android": writeText(f, " <Scope scope=\"android\" />")
  case "desktop": writeText(f, " <Scope scope=\"desktop\" />")
  case _: { }
}

def writeDescription(f, obj, lang, format = "%s") {
  str = getValue(obj, "desc", lang)
  if (str != "") {
    writeText(f, sprintf(format, str))
  }
}

def writeSince(f, version, lang, isInline = false) {
  if (isInline) {
    writeText(f, "<Since version=\"%s\" />".sprintf(version))
  } else {
    writeText(f, "<Since text=\"%s\" version=\"%s\" />".sprintf(messages.since[lang], version))
  }
}

// -- utils
def getValue(object, key, lang = "en") {
  newKey = (lang != "en") ? (key + "_" + lang) : key
  return object[newKey] ?? object[key] ?? ""
}

def header(text, level = 2) = ("#" * level) + " " + text

def readYml(filename) {
  f = fopen(filename, "r")
  s = readText(f)
  fclose(f)
  return yamldecode(s)
}